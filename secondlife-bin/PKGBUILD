# Maintainer: Jason Antman <jason@jasonantman.com>
pkgname=secondlife-beta-bin
pkgver=4.0.6.315555
_pkg_url_ver=$(echo "$pkgver" | sed 's|\.|_|g')
_pngver=1.5.1
pkgrel=1
pkgdesc="Second Life is a 3-D virtual world entirely built and owned by its residents (Binary version)"
url="http://www.secondlife.com/"
license=('GPL')
arch=('x86_64')
depends=('apr'
         'apr-util'
         'lib32-atk'
         'lib32-bzip2'
         'lib32-cairo'
         'lib32-expat'
         'lib32-fontconfig'
         'lib32-freetype2'
         'lib32-gcc-libs'
         'lib32-gdk-pixbuf2'
         'lib32-glib2'
         'lib32-glibc'
         'lib32-glu'
         'lib32-gtk2'
         'lib32-harfbuzz'
         'lib32-libdatrie'
         'lib32-libdrm'
         'lib32-libffi'
         'lib32-libpng'
         'lib32-libthai'
         'lib32-libx11'
         'lib32-libxau'
         'lib32-libxcb'
         'lib32-libxcomposite'
         'lib32-libxcursor'
         'lib32-libxdamage'
         'lib32-libxdmcp'
         'lib32-libxext'
         'lib32-libxfixes'
         'lib32-libxi'
         'lib32-libxinerama'
         'lib32-libxrandr'
         'lib32-libxrender'
         'lib32-libxshmfence'
         'lib32-libxxf86vm'
         'lib32-mesa'
         'lib32-mesa-libgl'
         'lib32-openal'
         'lib32-pango'
         'lib32-pcre'
         'lib32-pixman'
         'lib32-sdl'
         'lib32-wayland'
         'lib32-zlib')
optdepends=('lib32-libpulse: for PulseAudio support'
            'lib32-alsa-lib: for ALSA support'
            'lib32-nvidia-utils: for NVIDIA support'
            'lib32-flashplugin: for inworld Flash support'
            'freealut: unknown, possible dependency (ldd)'
            'hunspell: unknown, possible dependency (ldd)'
            'libfmod: unknown, possible dependency (ldd)')
conflicts=('secondlife' 'secondlife-svn')
install=secondlife.install
source=('secondlife.desktop' 'secondlife.launcher'
        'secondlife-64bitarch.patch'
        "http://download.cloud.secondlife.com/Viewer_4/Second_Life_${_pkg_url_ver}_i686.tar.bz2")
sha256sums=('187d4b7ceffddbf80a9074d2401d4ea7c85d9a5b18e3cb154684ca8a3b5a318e'
            'dea068b24298c76496df1be4007ef73976ffb1dc95ed61523a7236b72696c0c6'
            '166068e7f84ea81c8787065d2028c49adf7535e8597c2c23766e9d26d4d169d3'
            '0756f1d449e8134150a77f299acd9c0f811628456b2b1fe89036e6f314698518')

package() {
  cd $srcdir

  # Rename Data Directory
  mv Second_Life_${_pkg_url_ver}_i686 secondlife

  # patch launcher
  pushd secondlife && patch -p0 -i $srcdir/secondlife-64bitarch.patch && popd

  # Install Desktop File
  install -D -m644 $srcdir/secondlife.desktop \
    $pkgdir/usr/share/applications/secondlife.desktop

  # Install Icon File
  install -D -m644 $srcdir/secondlife/secondlife_icon.png \
    $pkgdir/usr/share/pixmaps/secondlife.png

  # Install Launcher
  install -D -m755 $srcdir/secondlife.launcher \
    $pkgdir/usr/bin/secondlife

  # Move Data to Destination Directory
  install -d $pkgdir/opt
  mv secondlife $pkgdir/opt/

  # Change Permissions of files to root:games
  chown -R root:games $pkgdir/opt/secondlife
  chmod -R g+rw $pkgdir/opt/secondlife

  # Make Binary Group-Executable
  chmod g+x $pkgdir/opt/secondlife/secondlife
}

# vim:set ts=2 sw=2 et:
